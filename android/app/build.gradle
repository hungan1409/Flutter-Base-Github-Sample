plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    // Workaround for support to multiple CIs (Bitrise and Codemagic).
    namespace "com.flutter.base"

    compileSdkVersion flutter.compileSdkVersion

    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false // issue https://github.com/flutter/flutter/issues/22397
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.flutter.base"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    // SigningConfigs
    apply from: 'signingConfigs/debug.gradle', to: android
    apply from: 'signingConfigs/release.gradle', to: android

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "environment"
    productFlavors {
        development {
            dimension "environment"
            applicationIdSuffix ".dev"
            versionNameSuffix "-Dev"
        }

        production {
            dimension "environment"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // Rename APK, get it in build/app/outputs/apk
            def versionCode = defaultConfig.versionCode
            def versionName = defaultConfig.versionName
            def date = new Date().format('yyyyMMdd-HHmm')
            if (variant.buildType.name.contains("debug")) {
                def newName = "${defaultConfig.applicationId}_v$versionName($versionCode)_${date}_${variant.productFlavors[0].name}_debug.apk"
                outputFileName = newName
            }
            if (name.contains("release")) {
                def newName = "${defaultConfig.applicationId}_v$versionName($versionCode)_${date}_${variant.productFlavors[0].name}_release.apk"
                outputFileName = newName
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
